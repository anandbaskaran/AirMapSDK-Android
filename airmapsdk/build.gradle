apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.airmap.airmapsdk'
def libVersion = '1.0.7'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "$libVersion"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile 'com.android.support.test:runner:0.5'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "org.robolectric:robolectric:3.1"

    def supportLibraryVersion = '25.3.1'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:support-fragment:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.github.bumptech.glide:glide:3.7.0' //Using Glide instead of Picasso because it is more efficient for the app's tutorial images
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.nakama.arraypageradapter:arraypageradapter:0.1.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'org.bitbucket.b_c:jose4j:0.5.5'
    compile 'com.google.protobuf:protobuf-java:3.2.0'
    compile 'joda-time:joda-time:2.9.7'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.3.2'
    compile('com.mapbox.mapboxsdk:mapbox-android-sdk:4.2.0@aar') { transitive = true }
    compile 'com.github.lzyzsd:jsbridge:1.0.4'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.1'
    compile('org.eclipse.paho:org.eclipse.paho.android.service:1.1.1') { exclude module: 'support-v4' }
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'online.devliving:securedpreferencestore:0.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
    compile 'com.ibm.icu:icu4j:58.2'
    compile 'com.auth0.android:lock:2.5.0'
}

configurations {
    // to avoid double inclusion of support libraries
    all*.exclude group: 'com.android.support', module: 'support-v4'
}

def siteUrl = 'http://www.airmap.com'
def projectName = 'com.airmap.airmapsdk'
def gitUrl = 'https://github.com/airmap/AirMapSDK-Android.git'

bintray {
    user = bintray_user
    key = bintray_api_key
    configurations = ['archives']
    pkg {
        repo = 'maven'
        userOrg = 'airmapio'
        version libVersion
        name = projectName
        desc = 'Create Flights, Send Telemetry Data, Get Realtime Traffic Alerts.'
        licenses = ['MIT']
        labels = ['android', 'airmap', 'drone', 'airspace', 'sdk', 'aar']
        vcsUrl = gitUrl
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name projectName
            url siteUrl
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/airmap/AirMapSDK-Android/blob/master/LICENSE'
                }
            }
            developers {
                developer {
                    id 'vanshg'
                    name 'Vansh Gandhi'
                    email 'vansh@airmap.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
